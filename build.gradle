buildscript {
	repositories {
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		mavenCentral()
		maven {
			name = "Jitpack"
			url 'https://jitpack.io/'
		}
		flatDir {
			dirs 'libs'
		}
	}
	dependencies {
		classpath 'com.github.calmilamsy:Fabric-Loom:d4e2c31'
	}
}

plugins {
	id 'java'
	id 'maven-publish'
}

compileJava {
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.compilerArgs << "-XDignore.symbol.file" << "-Xlint:unchecked"
}

sourceCompatibility = 1.8

apply plugin: "fabric-loom"

import net.fabricmc.loom.task.RunClientTask
import net.fabricmc.loom.task.RunServerTask

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

idea {
	module {
		downloadJavadoc = true
	}
}

eclipse {
	classpath {
		downloadJavadoc = true
	}
}

repositories {
	maven {
		name = "Glass"
		url "https://maven.glass-launcher.net/snapshots"
	}
	maven {
		name = "Forge"
		url "https://maven.minecraftforge.net/"
	}
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		name = "Jitpack"
		url "https://jitpack.io/"
	}
	mavenCentral()
}

minecraft {}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

task runTestmodClient(type: RunClientTask) {
	classpath sourceSets.testmod.runtimeClasspath
}

task runTestmodServer(type: RunServerTask) {
	classpath sourceSets.testmod.runtimeClasspath
}

dependencies {
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
	annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
	annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'

	// mixin and fabric loader need asm 9
	implementation 'org.ow2.asm:asm:9.2'
	implementation 'org.ow2.asm:asm-analysis:9.2'
	implementation 'org.ow2.asm:asm-commons:9.2'
	implementation 'org.ow2.asm:asm-tree:9.2'
	implementation 'org.ow2.asm:asm-util:9.2'

	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.fromCommit("calmilamsy/BIN-Mappings", "${project.yarn_mappings}") {spec ->
		spec.version = "b1.7.3-${project.yarn_mappings}"
	}

	// convenience stuff
	// adds some useful annotations for data classes. does not add any dependencies
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'

	// adds some useful annotations for miscellaneous uses. does not add any dependencies, though people without the lib will be missing some useful context hints.
	implementation 'org.jetbrains:annotations:22.0.0'

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.modificationstation:StationAPI:${project.stapi_version}"
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	// For tests only
	// modImplementation('com.github.calmilamsy:HowManyItems-Fabric-Unofficial:6af40ed') {
	// 	exclude group: 'net.modificationstation'
	// 	exclude group: 'com.github.modificationstation'
	// }
	//modImplementation "com.github.calmilamsy:ModMenu:dcef643"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	from javadoc
	classifier = 'javadoc'
}

tasks.publishToMavenLocal.dependsOn(tasks.remapJar)

publishing {
	repositories {
		mavenLocal()
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId project.archives_base_name
			artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") {
				classifier null
				builtBy remapJar
			}

			artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}-dev.jar") {
				classifier 'dev'
				builtBy remapJar
			}
		}
	}
}
